// Licensed to the Apache Software Foundation (ASF) under one
// or more contributor license agreements.  See the NOTICE file
// distributed with this work for additional information
// regarding copyright ownership.  The ASF licenses this file
// to you under the Apache License, Version 2.0 (the
// "License"); you may not use this file except in compliance
// with the License.  You may obtain a copy of the License at
//
//   http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.

apply plugin: "scala"
apply from: "$rootDir/gradle/shadow.gradle"

dependencies {
  // Note: We don't use the shaded version, so we can control the dependencies.
  implementation(project(path: ":kudu-backup-common")) {
    // Ensure we use the hadoop-client provided by Spark to avoid any compatibility issues.
    exclude group: "org.apache.hadoop", module: "hadoop-common"
    // Ensure we use the scala-library provided by Spark to avoid any compatibility issues.
    exclude group: "org.scala-lang", module: "scala-library"
  }
  implementation project(path: ":kudu-client", configuration: "shadow")
  implementation project(path: ":kudu-spark", configuration: "shadow")
  implementation libs.protobufJava
  implementation (libs.scopt)  {
    // Make sure wrong Scala version is not pulled in.
    exclude group: "org.scala-lang", module: "scala-library"
  }
  // TODO(KUDU-2500): Spark uses reflection which requires the annotations at runtime.
  implementation libs.yetusAnnotations

  implementation libs.scalaLibrary
  implementation libs.sparkAvro
  implementation libs.sparkCore
  implementation libs.sparkSql
  implementation libs.slf4jApi

  testImplementation project(path: ":kudu-test-utils", configuration: "shadow")
  testImplementation project(path: ":kudu-spark", configuration: "test")
  testImplementation libs.junit
  testImplementation libs.log4jApi
  testImplementation libs.log4jCore
  testImplementation libs.log4jSlf4jImpl
  testImplementation libs.scalatest
  testImplementation libs.logCaptor
}

shadowJar {
  dependencies {
    exclude(dependency(libs.jsr305))
    exclude(dependency("log4j:log4j:.*"))
    exclude(dependency("org.apache.arrow::.*"))
    exclude(dependency("org.apache.avro::.*"))
    exclude(dependency("org.apache.curator::.*"))
    exclude(dependency("org.apache.hadoop::.*"))
    exclude(dependency("org.apache.ivy:ivy::.*"))
    exclude(dependency("org.apache.parquet:.*"))
    exclude(dependency("org.apache.spark::.*"))
    exclude(dependency("org.codehaus.janino::.*"))
    exclude(dependency("org.glassfish.jersey.core::.*"))
    exclude(dependency("org.scala-lang::.*"))
    exclude(dependency("org.xerial.snappy::.*"))
  }
  exclude '**/*.adoc'
  exclude '**/*.default'
  exclude '**/*.dll'
  exclude '**/*.dylib'
  exclude '**/*.html'
  exclude '**/*.md'
  exclude 'META-INF/services/**'
  exclude 'META-INF/versions/11/org/roaringbitmap/**'
  exclude 'codegen/**'
  exclude 'assets/**'
  exclude 'javax/**'
  exclude 'org/jetbrains/**'

  minimize()
}

// Adjust the artifact name to match the maven build.
archivesBaseName = "kudu-backup${versions.sparkBase}_${versions.scalaBase}"

// kudu-backup has no public Javadoc.
javadoc {
  enabled = false
}