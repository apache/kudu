// Licensed to the Apache Software Foundation (ASF) under one
// or more contributor license agreements.  See the NOTICE file
// distributed with this work for additional information
// regarding copyright ownership.  The ASF licenses this file
// to you under the Apache License, Version 2.0 (the
// "License"); you may not use this file except in compliance
// with the License.  You may obtain a copy of the License at
//
//   http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.

apply plugin: "scala"
apply plugin: "java-library"
apply from: "$rootDir/gradle/shadow.gradle"

dependencies {
  implementation project(path: ":kudu-client", configuration: "shadow")
  implementation libs.hdrhistogram
  // TODO(KUDU-2500): Spark uses reflection which requires the annotations at runtime.
  implementation libs.yetusAnnotations

  compileOnlyApi libs.sparkCore
  compileOnlyApi libs.sparkSql

  compileOnlyApi libs.scalaLibrary
  compileOnlyApi libs.slf4jApi

  testImplementation project(path: ":kudu-test-utils", configuration: "shadow")
  testImplementation libs.junit
  testImplementation libs.scalatest
  testImplementation libs.log4jApi
  testImplementation libs.log4jCore
  testImplementation libs.log4jSlf4jImpl

  // Have to re-define some dependencies here, as compile-only dependencies
  // are not inherited by the test classpath.
  // See: https://blog.gradle.org/introducing-compile-only-dependencies
  // Note: compileUnshaded is extended from the compileOnly dependency scope.
  testImplementation libs.sparkCore
  testImplementation libs.sparkSql
  testImplementation libs.scalaLibrary
  testImplementation libs.slf4jApi
}

shadowJar {
  dependencies {
    exclude(dependency("com.fasterxml.jackson.core::.*"))
    exclude(dependency("com.github.luben::.*"))
    exclude(dependency("javax.servlet.jsp::.*"))
    exclude(dependency("log4j:log4j::.*"))
    exclude(dependency("org.apache.avro::.*"))
    exclude(dependency("org.apache.commons::.*"))
    exclude(dependency("org.apache.curator::.*"))
    exclude(dependency("org.apache.hadoop::.*"))
    exclude(dependency("org.apache.ivy:ivy::.*"))
    exclude(dependency("org.apache.parquet::.*"))
    exclude(dependency("org.apache.spark::.*"))
    exclude(dependency("org.codehaus.janino::.*"))
    exclude(dependency("org.glassfish.jersey.core::.*"))
    exclude(dependency("org.scala-lang::.*"))
    exclude(dependency("org.xerial.snappy::.*"))
  }
  exclude '**/*.adoc'
  exclude '**/*.default'
  exclude '**/*.dll'
  exclude '**/*.dylib'
  exclude '**/*.html'
  exclude '**/*.md'
  exclude 'codegen/**'
  exclude 'org/threeten/**'
  exclude 'org/apache/arrow/**'
  exclude 'org/apache/orc/**'
  exclude 'javax/**'
  exclude 'org/jetbrains/**'
  exclude 'org/joda/time/tz/**'

  minimize()
}

// Adjust the artifact name to include the spark and scala base versions.
archivesBaseName = "kudu-spark${versions.sparkBase}_${versions.scalaBase}"

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
  // This class causes SpotBugs runtime errors, so we completely ignore it from analysis.
  classes = classes.filter { !it.path.contains("SparkSQLTest") }
}
